pluginManagement {
    if (!gradle.root.hasProperty("mozconfig")){
        apply from: file('./gradle/mozconfig.gradle')
    } else {
        gradle.ext.mozconfig = gradle.root.mozconfig
    }

    repositories {
        gradle.mozconfig.substs.GRADLE_MAVEN_REPOSITORIES.each { repository ->
            maven {
                url = repository
                if (gradle.mozconfig.substs.ALLOW_INSECURE_GRADLE_REPOSITORIES) {
                    allowInsecureProtocol = true
                }
            }
        }
    }

    includeBuild("./android-components/plugins/publicsuffixlist")
    includeBuild("./android-components/plugins/dependencies")
    includeBuild("./android-components/plugins/config")
    includeBuild("./plugins/apksize")
    includeBuild("./plugins/nimbus-gradle-plugin")
}

plugins {
    id 'mozac.DependenciesPlugin'
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
    }
}

ext.topsrcdir = rootProject.projectDir.absolutePath.minus("mobile/android/fenix")

apply from: file('./shared-settings.gradle')

if (gradle.ext.mozconfig.substs.MOZ_APPSERVICES_IN_TREE) {
    apply from: file("${topsrcdir}/services/app-services/settings.gradle")
}

include ':app'
include ':mozilla-detekt-rules'
include ':benchmark'

gradle.projectsLoaded { ->
    gradle.allprojects { project ->
        if (project.projectDir.absolutePath.contains("/android-components/")) {
            project.tasks.withType(Test).configureEach { enabled = false }
            project.tasks.configureEach { task ->
                if (task.name.contains("lint")) { task.enabled = false }
            }
        }
    }
}
