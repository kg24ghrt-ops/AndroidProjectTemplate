/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

pluginManagement {
    if (!gradle.root.hasProperty("mozconfig")){
        apply from: file('./gradle/mozconfig.gradle')
    } else {
        gradle.ext.mozconfig = gradle.root.mozconfig
    }

    repositories {
        gradle.mozconfig.substs.GRADLE_MAVEN_REPOSITORIES.each { repository ->
            maven {
                url = repository
                if (gradle.mozconfig.substs.ALLOW_INSECURE_GRADLE_REPOSITORIES) {
                    allowInsecureProtocol = true
                }
            }
        }
    }

    // UPDATED: Pointing to ac-source where files are now physically tracked
    includeBuild("./ac-source/plugins/publicsuffixlist")
    includeBuild("./ac-source/plugins/dependencies")
    includeBuild("./ac-source/plugins/config")
    includeBuild("./plugins/apksize")
    includeBuild("./plugins/nimbus-gradle-plugin")
}

plugins {
    id 'mozac.DependenciesPlugin'
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
    }
}

ext.topsrcdir = rootProject.projectDir.absolutePath.minus("mobile/android/fenix")

apply from: file('./shared-settings.gradle')

if (gradle.ext.mozconfig.substs.MOZ_APPSERVICES_IN_TREE) {
    apply from: file("${topsrcdir}/services/app-services/settings.gradle")
}

include ':app'
include ':mozilla-detekt-rules'
include ':benchmark'

gradle.projectsLoaded { ->
    // UPDATED: Disables tests/lint in the new ac-source path
    gradle.allprojects { project ->
        if (project.projectDir.absolutePath.contains("/ac-source/")) {
            project.tasks.withType(Test).configureEach { enabled = false }
            project.tasks.configureEach { task ->
                if (task.name.contains("lint")) { task.enabled = false }
            }
        }
    }
}

def projectLocalProperties = file("local.properties").with { localPropertiesFile ->
    def localProperties = new Properties()
    if (localPropertiesFile.canRead()) {
        localPropertiesFile.withInputStream { localProperties.load(it) }
    }
    localProperties
}

projectLocalProperties.each { prop ->
    gradle.ext."localProperties.${prop.key}" = prop.value
}
